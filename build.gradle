plugins {
    id 'java'
    id 'jacoco'
}

group 'com.nvrsk'
version '1.0'
//sourceCompatibility = '18'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

jacoco {
    toolVersion = "0.8.8"
}


tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport { // generates code coverage report for the test task
    dependsOn test // tests are required to run before generating the report

    reports {
        xml.required = true
        csv.required = true
//        html will be generated by default
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/nvrsk/main/**',
                              'com/nvrsk/package2/**']
            )
        }))
    }
}

jacocoTestCoverageVerification { // verifies code coverage metrics based on specified rules for the test task
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/nvrsk/main/**',
                              'com/nvrsk/package2/**']
            )
        }))
    }
}

// to run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification
